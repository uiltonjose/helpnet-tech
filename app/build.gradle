apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.helpnet.tech"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 10001
        versionName "1.0.1"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        setProperty("archivesBaseName", "HN-Tech_$versionName")
    }
    signingConfigs {
        release {
            if (project.hasProperty('MYAPP_RELEASE_STORE_FILE')) {
                storeFile file(MYAPP_RELEASE_STORE_FILE)
                storePassword MYAPP_RELEASE_STORE_PASSWORD
                keyAlias MYAPP_RELEASE_KEY_ALIAS
                keyPassword MYAPP_RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    flavorDimensions "server"
    productFlavors {
        dev {
            buildConfigField "String", "BASE_API_URL", '"https://helpnetws.herokuapp.com/api/"'
            dimension "server"
            resConfigs "en", "xxhdpi"
        }
        qa {
            buildConfigField "String", "BASE_API_URL", '"https://helpnetqa.herokuapp.com/api/"'
            dimension "server"
        }
        prod {
            buildConfigField "String", "BASE_API_URL", '"https://helpnetprod.herokuapp.com/api/"'
            dimension "server"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation Libs.androidXAppCompat
    implementation Libs.androidXCore
    implementation Libs.constraintLayout
    implementation Libs.material

    //Kotlin
    implementation Libs.kotlin
    //Coroutines
    implementation Libs.coroutinesCore
    implementation Libs.coroutinesAndroid

    implementation Libs.multidex

    // Test
    androidTestImplementation Libs.instrumentationRunner
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    testImplementation Libs.junit

    // GSON
    implementation Libs.gson

    //Retrofit Api
    implementation Libs.retrofit
    implementation Libs.retrofitConverter
    implementation Libs.okHttp
    implementation Libs.okHttpLogginInterceptor

    // Glide
    implementation Libs.glide

    // Better Spinner
    implementation(Libs.betterSpinner) {
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // OneSignal (Push notification)
    //implementation 'com.onesignal:OneSignal:[3.9.1, 3.99.99]'

    // LifeCycle
    implementation Libs.androidArchLifecycle

    // Firebase
    implementation Libs.firebaseCore
    implementation Libs.firebaseAuth

    // Leak Canary
    debugImplementation Libs.leakCanaryDebug
    releaseImplementation Libs.leakCanaryRelease

    //CircleImageView
    implementation Libs.circleImageView

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'